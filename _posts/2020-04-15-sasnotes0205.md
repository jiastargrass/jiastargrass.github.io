---
layout: posts_nologo
title: "SAS2-5 Combining Tables"
updated: 2020-04-14 10:30
categories: sas
author: "Jason"
permalink: /:categories/:year:month:day/:title.html
---

##Combining Tables

### Concatenating Tables

- **DATA** *output-table***;**     **SET** *input-table1***(rename=(** *current-colname*=*new-colname***))**          *input-table2* **...;** **RUN;**

```sas
*Example 1;
data class_current;
	length name $ 9;
    set sashelp.class pg2.class_new2(rename=(student=name));
run;

*Example 2;
data work.np_combine_new;
	set pg2.np_2014(rename=(type=parktype park=parkcode)) pg2.np_2015 pg2.np_2016;
	CampTotal = sum(of Camping:);
	where month in (6, 7, 8) and ParkType = "National Park";
	format camptotal comma15.;
    drop Camping:;
run;
```

- Multiple tables listed in the SET statement are concatenated.
- SAS first reads all the rows from the first table listed in the SET statement and writes them to the new table. Then it reads and writes the rows from the second table, and so on.
- Columns with the same name are automatically aligned. The column properties in the new table are inherited from the first table that is listed in the SET statement.
- Columns that are not in all tables are also included in the output table.
- The RENAME= data set option can be used to rename columns in one or both tables so that they align in the new table.
- Additional DATA step statements can be used after the SET statement to manipulate the data.

### Merging Tables

- *If data needs to be sorted prior to the merge:*

- **PROC SORT DATA=** *input-table* **OUT=** *output-table***;**      **BY** *BY-column***;** **RUN;**

- **DATA** *output-table***;**      **MERGE** *input-table1 input-table2 ...***;**      **BY** *BY-column(s)***;** **RUN;**

- Any tables listed in the MERGE statement must be sorted by the same column (or columns) listed in the BY statement.
- The MERGE statement combines rows where the BY-column values match.
- This syntax merges multiple tables in both one-to-one and one-to-many situations.

### Identifying Matching and Nonmatching Rows

- **DATA** *output-table***;**      **MERGE** *input-table1*(**IN**=*var1*) *input-table2*(**IN**=*var2*) ...**;**      **BY** *BY-column(s)***;** **RUN;**

```sas
data class2;
    merge pg2.class_update(in=inUpdate) 
          pg2.class_teachers(in=inTeachers);
    by name;
    if inUpdate=1 and inTeachers=1;
run;
```



- By default, both matches and nonmatches are written to the output table in a DATA step merge.
- The IN= data set option follows a table in the MERGE statement and names a variable that will be added to the PDV. The IN= variables are included in the PDV during execution, but they are not written to the output table. Each IN= variable relates to the table that the option follows.
- During execution, the IN= variable is assigned a value of 0 or 1. 0 means that the corresponding table did ***not\*** include the BY column value for that row, and 1 means that it did include the BY-column value.
- The subsetting IF or IF-THEN logic can be used to subset rows based on matching or nonmatching rows.



```sas
/*Steps a and b*/
proc sort data=pg2.np_CodeLookup out=sortnames(keep=ParkName ParkCode);
	by ParkName;
run;

proc sort data=pg2.np_final out=sortfinal;
	by ParkName;
run;

data highuse(keep=ParkCode ParkName);
	merge sortfinal sortnames;
	by ParkName;
	*Add a subsetting IF statement;
	if DayVisits >= 5000000;
run;

/*Step c*/
proc sort data=pg2.np_species
	  	  out=birds(keep=ParkCode Species_ID Scientific_Name Common_Names);
	by ParkCode Species_ID;
	where Category='Bird' and Abundance='Common';
run;

proc sort data=highuse;
	by parkcode;
run;

data birds_largepark;
	merge highuse(in=inhighuse) birds;
	by parkcode;
	if inhighuse=1;
run;
```

