---
layout: posts_nologo
title: "SAS笔记2"
updated: 2020-04-06 15:56
categories: sas
author: "Jason"
permalink: /:categories/:year:month:day/:title.html
---
**DATA Step Processing**  

**1 - Compilation**  

Establish data attributes and rules for execution  

* Check for syntax errors
* Create the program data vector (PDV), containing columns and attributes (this is way length statement has to be before if statements)
* Establish rules for processing data in the PDV
* Create descriptor portion of output table  
  (note: drop statement does not drop the column immediately, SAS makes a flag for that column and drops it later)  

**2 - Execution**  

read, manipulate, and write DATA    
(简单说,就是把这个section看作一个对每一行observation进行iteration)  

To view the DATA step processing steps, use the **DATA Step Debugger**

**An Example:**

```SAS
data np_parks;
	set pg2.np_final(obs=5);
	putlog "NOTE: Star data step iteration";
	putlog _all_;
	keep Region ParkName AvgMonthlyVisitors Acres Size; *compile only;
    where Type="PARK"; *compile only;
	format AvgMonthlyVisitors Acres comma10.;*compile only;
	length Size $ 6;
    Type=propcase(Type);
	putlog Type=;
	AvgMonthlyVisitors=sum(DayVisits,Campers,OtherLodging)/12;
	if Acres<1000 then Size="Small";
	else if Acres<100000 then Size="Medium";
	else Size="Large";
	putlog "NOTE: End of Data step";
	putlog _all_;
run;
```

Note: the putlog statements can show the data processing details

**Directing DATA Step Output**

```SAS
data forecast;
	set sashelp.shoes;
	keep Region Product Subsidiary Year ProjectedSales;
	format ProjectedSales dollar10.;
    Year=1;
	ProjectedSales=Sales*1.05;
    output;
	Year=2;
	ProjectedSales=ProjectedSales*1.05;
    output;
	Year=3;
	ProjectedSales=ProjectedSales*1.05;
	/*	output;*/
run;
```

When an explicit OUTPUT statement is in a DATA step, the implicit OUTPUT is disabled.

**Create multiple output tables**

```SAS
data indian atlantic pacific;
	set pg2.storm_summary;
	length Ocean $ 8;
	Basin=upcase(Basin);
	StormLength=EndDate-StartDate;
	MaxWindKM=MaxWindMPH*1.60934;

	if substr(Basin,2,1)="I" then
		do;
			Ocean="Indian";
			output indian;
		end;
	else if substr(Basin,2,1)="A" then
		do;
			Ocean="Atlantic";
			output atlantic;
		end;
	else
		do Ocean="Pacific";
			output pacific;
		end;
	drop MaxWindMPH;

run;
```

**Directing column output**

```SAS
data indian(drop=MaxWindMPH) atlantic(drop=MaxWindKM) pacific;
	set pg2.storm_summary(drop=MinPressure);
	length Ocean $ 8;
	Basin=upcase(Basin);
	StormLength=EndDate-StartDate;
	MaxWindKM=MaxWindMPH*1.60934;
	if substr(Basin,2,1)="I" then do;
		Ocean="Indian";
		output indian;
	end;
	else if substr(Basin,2,1)="A" then do;
		Ocean="Atlantic";
		output atlantic;
	end;
	else do;
		Ocean="Pacific";
		output pacific;
	end;
run;
```

**用select代替if else**

```SAS
data monument(drop=ParkType) park(drop=ParkType) other;
	set pg2.np_yearlytraffic;
	select (ParkType);
		when ('National Monument') do; output monument; end;
		when ('National Park') do; output park;end;
		otherwise do; output other; end;
	end;
	drop Region;
run;
```
