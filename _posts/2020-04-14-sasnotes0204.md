---
layout: posts_nologo
title: "SAS2-4 Creating Custom Formats"
updated: 2020-04-14 10:30
categories: sas
author: "Jason"
permalink: /:categories/:year:month:day/:title.html
---
## Creating and Using Custom Formats

- **PROC FORMAT;**     

- ​	**VALUE** *format-name value-or-range-1* = '*formatted-value*'                      
  ​									    *value-or-range-2* = '*formatted-value*' 
  ​				                        . . . **;** 
  **RUN;**

- ```sas
  proc format;
      value $genfmt 'F'='Female'
                    'M'='Male'
  				  other = 'Miscoded'; /* any other values */
  
      value HRANGE low-<58 = 'Below Average' /* < means excluding the ending value, low means the lowest possible value */
                   58-60* = 'Average'       /* 60* means 'includes', 58 is already included */
                   60<-high = 'Above Average' ; /* > means excluding the starting value, high means the highest possible value */
  run;
  
  
  ```

- 

- The FORMAT procedure is used to create custom formats.
- A VALUE statement specifies the criteria for creating one custom format.
- Multiple VALUE statements can be used in the PROC FORMAT step.
- The format name can be up to 32 characters in length, must begin with a $ followed by a letter or underscore for character formats, and must begin with a letter or underscore for numeric formats.
- On the left side of the equal sign, you specify individual values or a range of values that you want to convert to formatted values. Character values must be in quotation marks; numeric values are not quoted.
- On the right side of the equal sign, you specify the formatted values that you want the values on the left side to become. Formatted values need to be in quotation marks.
- The keywords LOW, HIGH, and OTHER can be used in the VALUE statement.
- You do not include a period in the format name when you create the format, but you do include the period in the name when you use the format.
- Custom formats can be used in the FORMAT statement and the PUT function.



## Creating Custom Formats from Tables

- **PROC FORMAT CNTLIN=***input-table* **FMTLIB;**       **SELECT** *format-names***;** **RUN;**

- The CNTLIN= option specifies a table from which PROC FORMAT builds formats.

- The input table <u>**must**</u> contain at a minimum three character columns:

- - **Start**, which represents the raw data values to be formatted.
  - **Label**, which represents the formatted labels.
  - **FmtName**, which contains the name of the format that you are creating. Character formats start with a dollar sign.
  - If specifying ranges, we need an **End** columns as well.

**Examples:**

```SAS
/*Create the $SBFMT format for subbasin codes*/
data sbdata;
    retain FmtName '$sbfmt';
    set pg2.storm_subbasincodes(rename=(Sub_Basin=Start 
                                        SubBasin_Name=Label));
    keep Start Label FmtName;
run;

proc format cntlin=sbdata;
run;

/*Create the CATFMT format for storm categories*/
data catdata;
    retain FmtName "catfmt";
    set pg2.storm_categories(rename=(Low=Start 
                                     High=End
                                     Category=Label));
    keep FmtName Start End Label;
run;

proc format cntlin=catdata;
run;

/*Generate a report for selected formats in the WORK library*/
proc format fmtlib library=work;
select $sbfmt catfmt;
run;
```

## Storing Custom Formats

```SAS
/* Save the formats to the pg2.formats catalog */
proc format LIBRARY=pg2.formats;
    value $gender 'F'='Female'
                  'M'='Male'
                  other='Miscoded';
    value hght low-<58  = 'Below Average'
                58-60   = 'Average'
               60<-high = 'Above Average';
run;

/* Add an OPTIONS statement so that SAS can find the two permanent formats */
OPTIONS fmtsearch=(pg2.formats);

proc print data=pg2.class_birthdate noobs;
    where Age=12;
    var Name Gender Height;
    format Gender $gender. Height hght.;
run;
```

