---
layout: posts_nologo
title: "SAS2-2 Summarizing Data"
updated: 2020-04-12 14:30
categories: sas
author: "Jason"
permalink: /:categories/:year:month:day/:title.html
---
**Summarizing Data**

Creating an Accumulating Column (use of retain - set the initial value and accumulate)

In SAS, a missing value + any value = missing value

Using <u>**retain**</u> to retain the accumulation and set initial number

Using **<u>SUM function</u>** can solve the problem as it ignores missing values.

```SAS
data houston_rain;
	set pg2.weather_houston;
	keep Date DailyRain YTDRain;
	retain YTDRain 0;
	YTDRain=sum(YTDRain,DailyRain);
run;
```

A simpler way:

```sas
data zurich2017;
    set pg2.weather_zurich;
    YTDRain_mm+Rain_mm;
    DayNum+1;
run;
```

DATA step can process within groups (with temporary variables first. last.)

```sas
proc sort data=pg2.storm_2017 out=storm2017_sort(keep=Basin Name);
	by Basin;
run;

data storm2017_max;
	set storm2017_sort;
	by Basin;
	First_Basin=first.basin;
	Last_Basin=last.basin;
run;
```

Subsetting rows in the Execution phase example (将if尽量放在前面,可节省运算量):

```sas
data storm2017_max others;
	set storm2017_sort;
	by Basin;
	if last.Basin = 1;
	StormLength=EndDate-StartDate;
	MaxWindKM=MaxWindMPH*1.60934;
	First_Basin=first.Basin;
	Last_Basin=last.Basin;
run;
```

Use first. or last. to add a variable accumulating subsets:

```sas
data houston_monthly;
	set pg2.weather_houston;
	keep Date Month DailyRain MTDRain;
	by Month;
	if first.Month=1 then MTDRain=0;
	MTDRain+DailyRain;
	if last.month=1;
run;       
```

Example: Show the sum of sales of product in each region

```sas
proc sort data=sashelp.shoes out=shoes;
	by Region Product;
run;

data profitsummary;
	set shoes;
	by Region Product;
	Profit = Sales - Returns;
	if first.Product = 1 then TotalProfit = 0;
	TotalProfit + Profit;
	if last.Product = 1;
	keep Region Product TotalProfit;
	format TotalProfit DOLLAR15.2;
run;
```

